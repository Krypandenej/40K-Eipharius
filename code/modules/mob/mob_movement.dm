/mob/CanPass(atom/movable/mover, turf/target, height=0, air_group=0)
	if(air_group || (height==0)) return 1

	if(ismob(mover))
		var/mob/moving_mob = mover
		if ((other_mobs && moving_mob.other_mobs))
			return 1
		return (!mover.density || !density || lying)
	else
		return (!mover.density || !density || lying)
	return

/mob/proc/setMoveCooldown(var/timeout)
	if(client)
		client.move_delay = max(world.time + timeout, client.move_delay)

/client/North()
	..()


/client/South()
	..()


/client/West()
	..()


/client/East()
	..()


/client/proc/client_dir(input, direction=-1)
	return turn(input, direction*dir2angle(dir))

/client/Northeast()
	diagonal_action(NORTHEAST)
/client/Northwest()
	diagonal_action(NORTHWEST)
/client/Southeast()
	diagonal_action(SOUTHEAST)
/client/Southwest()
	diagonal_action(SOUTHWEST)

/client/proc/diagonal_action(direction)
	switch(client_dir(direction, 1))
		if(NORTHEAST)
			swap_hand()
			return
		if(SOUTHEAST)
			attack_self()
			return
		if(SOUTHWEST)
			if(iscarbon(usr))
				var/mob/living/carbon/C = usr
				C.toggle_throw_mode()
			else
				to_chat(usr, "<span class='warning'>This mob type cannot throw items.</span>")
			return
		if(NORTHWEST)
			mob.hotkey_drop()

/mob/proc/hotkey_drop()
	to_chat(usr, "<span class='warning'>This mob type cannot drop items.</span>")

/mob/living/carbon/hotkey_drop()
	if(!get_active_hand())
		to_chat(usr, "<span class='warning'>You have nothing to drop in your hand.</span>")
	else
		drop_item()

//This gets called when you press the delete button.
/client/verb/delete_key_pressed()
	set hidden = 1

	if(!usr.pulling)
		to_chat(usr, "<span class='notice'>You are not pulling anything.</span>")
		return
	usr.stop_pulling()

/client/verb/swap_hand()
	set hidden = 1
	if(istype(mob, /mob/living/carbon))
		mob:swap_hand()
	if(istype(mob,/mob/living/silicon/robot))
		var/mob/living/silicon/robot/R = mob
		R.cycle_modules()
	return



/client/verb/attack_self()
	set hidden = 1
	if(mob)
		mob.mode()
	return


/client/verb/toggle_throw_mode()
	set hidden = 1
	if(!istype(mob, /mob/living/carbon))
		return
	if (!mob.stat && isturf(mob.loc) && !mob.restrained())
		mob:toggle_throw_mode()
	else
		return


/client/verb/drop_item()
	set hidden = 1
	if(!isrobot(mob) && mob.stat == CONSCIOUS && isturf(mob.loc))
		return mob.drop_item()
	return


/client/Center()
	/* No 3D movement in 2D spessman game. dir 16 is Z Up
	if (isobj(mob.loc))
		var/obj/O = mob.loc
		if (mob.canmove)
			return O.relaymove(mob, 16)
	*/
	return

//This proc should never be overridden elsewhere at /atom/movable to keep directions sane.
/atom/movable/Move(newloc, direct)
	if (direct & (direct - 1))
		if (direct & 1)
			if (direct & 4)
				if (step(src, NORTH))
					step(src, EAST)
				else
					if (step(src, EAST))
						step(src, NORTH)
			else
				if (direct & 8)
					if (step(src, NORTH))
						step(src, WEST)
					else
						if (step(src, WEST))
							step(src, NORTH)
		else
			if (direct & 2)
				if (direct & 4)
					if (step(src, SOUTH))
						step(src, EAST)
					else
						if (step(src, EAST))
							step(src, SOUTH)
				else
					if (direct & 8)
						if (step(src, SOUTH))
							step(src, WEST)
						else
							if (step(src, WEST))
								step(src, SOUTH)
	else
		var/atom/A = src.loc

		var/olddir = dir //we can't override this without sacrificing the rest of movable/New()
		. = ..()
		if(direct != olddir)
			dir = olddir
			set_dir(direct)

		src.move_speed = world.time - src.l_move_time
		src.l_move_time = world.time
		src.m_flag = 1
		if ((A != src.loc && A && A.z == src.z))
			src.last_move = get_dir(A, src.loc)
	return

/client/proc/Move_object(direct)
	if(mob && mob.control_object)
		if(mob.control_object.density)
			step(mob.control_object,direct)
			if(!mob.control_object)	return
			mob.control_object.set_dir(direct)
		else
			mob.control_object.forceMove(get_step(mob.control_object,direct))
	return


/client/Move(n, direct)
	if(!mob)
		return // Moved here to avoid nullrefs below

	if(mob.control_object)	Move_object(direct)

	if(mob.incorporeal_move && isobserver(mob))
		Process_Incorpmove(direct)
		return

	if(moving)	return 0

	if(world.time < move_delay)	return

	if(locate(/obj/effect/stop/, mob.loc))
		for(var/obj/effect/stop/S in mob.loc)
			if(S.victim == mob)
				return

	if(mob.stat==DEAD && isliving(mob))
		mob.ghostize()
		return

	// handle possible Eye movement
	if(mob.eyeobj)
		return mob.EyeMove(n,direct)

	if(mob.transforming)	return//This is sota the goto stop mobs from moving var

	if(Process_Grab())	return

	if(!mob.canmove)
		return

	if(isliving(mob))
		var/mob/living/L = mob
		if(L.incorporeal_move)//Move though walls
			Process_Incorpmove(direct)
			return
		if(mob.client)
			if(mob.client.view != world.view) // If mob moves while zoomed in with device, unzoom them.
				for(var/obj/item/item in mob.contents)
					if(item.zoom)
						item.zoom(mob)
						break

	if(!mob.lastarea)
		mob.lastarea = get_area(mob.loc)

	if(!mob.check_solid_ground())
		var/allowmove = mob.Allow_Spacemove(0)
		if(!allowmove)
			return 0
		else if(allowmove == -1 && mob.handle_spaceslipping()) //Check to see if we slipped
			return 0
		else
			mob.inertia_dir = 0 //If not then we can reset inertia and move

	if(isobj(mob.loc) || ismob(mob.loc))//Inside an object, tell it we moved
		var/atom/O = mob.loc
		return O.relaymove(mob, direct)

	if(isturf(mob.loc))

		if(mob.restrained())//Why being pulled while cuffed prevents you from moving
			for(var/mob/M in range(mob, 1))
				if(M.pulling == mob)
					if(!M.restrained() && M.stat == 0 && M.canmove && mob.Adjacent(M))
						to_chat(src, "<span class='notice'>You're restrained! You can't move!</span>")
						return 0
					else
						M.stop_pulling()

		if(mob.pinned.len)
			to_chat(src, "<span class='notice'>You're pinned to a wall by [mob.pinned[1]]!</span>")
			return 0

		move_delay = world.time//set move delay

		switch(mob.m_intent)
			if("run")
				if(mob.drowsyness > 0)
					move_delay += 3
				move_delay += 1.5+config.run_speed
			if("walk")
				move_delay += 5+config.walk_speed
		move_delay += mob.movement_delay()

		if(istype(mob.buckled, /obj/vehicle))
			//manually set move_delay for vehicles so we don't inherit any mob movement penalties
			//specific vehicle move delays are set in code\modules\vehicles\vehicle.dm
			move_delay = world.time
			//drunk driving
			if(mob.confused && prob(20)) //vehicles tend to keep moving in the same direction
				direct = turn(direct, pick(90, -90))
			return mob.buckled.relaymove(mob,direct)

		if(istype(mob.machine, /obj/machinery))
			if(mob.machine.relaymove(mob,direct))
				return

		if(mob.pulledby || mob.buckled) // Wheelchair driving!
			if(istype(mob.loc, /turf/space))
				return // No wheelchair driving in space
			if(istype(mob.pulledby, /obj/structure/bed/chair/wheelchair))
				return mob.pulledby.relaymove(mob, direct)
			else if(istype(mob.buckled, /obj/structure/bed/chair/wheelchair))
				if(ishuman(mob))
					var/mob/living/carbon/human/driver = mob
					var/obj/item/organ/external/l_hand = driver.get_organ(BP_L_HAND)
					var/obj/item/organ/external/r_hand = driver.get_organ(BP_R_HAND)
					if((!l_hand || l_hand.is_stump()) && (!r_hand || r_hand.is_stump()))
						return // No hands to drive your chair? Tough luck!
				//drunk wheelchair driving
				else if(mob.confused)
					switch(mob.m_intent)
						if("run")
							if(prob(50))	direct = turn(direct, pick(90, -90))
						if("walk")
							if(prob(25))	direct = turn(direct, pick(90, -90))
				move_delay += 2
				return mob.buckled.relaymove(mob,direct)

		if(mob.check_slipmove())
			return

		if(ishuman(mob))//The worst possible way of stoping people with broken legs from walking.
			if(GLOB.waddling)//Admeme
				mob.waddle()
			//if(mob.zoomed) //Going to allow people to move while zoomed in for now.
			//	mob.do_zoom()
			var/mob/living/carbon/human/H = mob
			if(H.shadow)
				if(H.client.eye == H.shadow)
					H.reset_view(0)
			var/obj/item/organ/external/l_leg = H.get_organ(BP_L_LEG)
			var/obj/item/organ/external/r_leg = H.get_organ(BP_R_LEG)
			var/obj/item/organ/external/l_foot = H.get_organ(BP_L_FOOT)
			var/obj/item/organ/external/r_foot = H.get_organ(BP_R_FOOT)

			if(!H.lying && !H.buckled)
				if(!istype(H.get_hands(), /obj/item/crutches))
					if(!l_leg || l_leg.is_stump() || l_leg.is_broken())
						to_chat(H, "<span class='warning'>You're crippled you can't walk.</span>")
						H.Weaken(1)
						return
					if(!r_leg || r_leg.is_stump() || r_leg.is_broken())
						to_chat(H, "<span class='warning'>You're crippled you can't walk.</span>")
						H.Weaken(1)
						return
					if(!l_foot || l_foot.is_stump() || l_foot.is_broken())
						to_chat(H, "<span class='warning'>You're crippled you can't walk.</span>")
						H.Weaken(1)
						return
					if(!r_foot || r_foot.is_stump() || r_foot.is_broken())
						to_chat(H, "<span class='warning'>You're crippled you can't walk.</span>")
						H.Weaken(1)
						return
			/*
			if(istype(H.loc, /turf/simulated/floor/trench))//Hacky, but effective.
				if((!istype(n, /turf/simulated/floor/trench) && !istype(n, /turf/simulated/open)) && !H.pulledby)//If we're exiting the trench, and we're not being pulled.
					H.visible_message("[H] begins to climb out of the trench.")
					if(!do_after(H,10))
						H.visible_message("[H] fails to climb out of the trench.")
						if(H.crouching)//Stand us back up.
							H.toggle_crouch()
						return
			*/

		//We are now going to move
		moving = 1
		//Something with pulling things
		if(locate(/obj/item/grab, mob))
			for (var/obj/item/grab/G in mob)
				move_delay = max(move_delay, world.time + G.grab_slowdown())
				var/list/L = mob.ret_grab()
				if(istype(L, /list))
					if(L.len == 2)
						L -= mob
						var/mob/M = L[1]
						if(M)
							if ((get_dist(mob, M) <= 1 || M.loc == mob.loc))
								var/turf/T = mob.loc
								. = ..()
								if (isturf(M.loc))
									var/diag = get_dir(mob, M)
									if ((diag - 1) & diag)
									else
										diag = null
									if ((get_dist(mob, M) > 1 || diag))
										step(M, get_dir(M.loc, T))
					else
						for(var/mob/M in L)
							M.other_mobs = 1
							if(mob != M)
								M.animate_movement = 3
						for(var/mob/M in L)
							spawn( 0 )
								step(M, direct)
								return
							spawn( 1 )
								M.other_mobs = null
								M.animate_movement = 2
								return
					G.adjust_position()
		else
			if(mob.confused)
				switch(mob.m_intent)
					if("run")
						if(prob(75))
							direct = turn(direct, pick(90, -90))
							n = get_step(mob, direct)
					if("walk")
						if(prob(25))
							direct = turn(direct, pick(90, -90))
							n = get_step(mob, direct)
			. = mob.SelfMove(n, direct)

		for (var/obj/item/grab/G in mob)
			if (G.assailant_reverse_facing())
				mob.set_dir(GLOB.reverse_dir[direct])
			G.assailant_moved()
		for (var/obj/item/grab/G in mob.grabbed_by)
			G.adjust_position()

		moving = 0

		if(mob.pulling)
			mob.dir = turn(mob.dir, 180)
			mob.update_vision_cone()

		if((direct & (direct - 1)) && mob.loc == n) //moved diagonally successfully
			move_delay += 2

		return .

	return

/mob/proc/SelfMove(turf/n, direct)
	return Move(n, direct)


///Process_Incorpmove
///Called by client/Move()
///Allows mobs to run though walls
/client/proc/Process_Incorpmove(direct)
	if(mob.confused)
		switch(mob.m_intent)
			if("run")
				if(prob(75))
					direct = turn(direct, pick(90, -90))
			if("walk")
				if(prob(25))
					direct = turn(direct, pick(90, -90))

	var/turf/T = get_step(mob, direct)
	if(mob.check_is_holy_turf(T))
		to_chat(mob, "<span class='warning'>You cannot enter holy grounds while you are in this plane of existence!</span>")
		return

	if(T)
		mob.forceMove(T)
	mob.set_dir(direct)

	mob.Post_Incorpmove()
	return 1

/mob/proc/Post_Incorpmove()
	return

// Checks whether this mob is allowed to move in space
// Return 1 for movement, 0 for none,
// -1 to allow movement but with a chance of slipping
/mob/proc/Allow_Spacemove(var/check_drift = 0)
	if(!Check_Dense_Object()) //Nothing to push off of so end here
		return 0

	if(restrained()) //Check to see if we can do things
		return 0

	return -1

//Checks if a mob has solid ground to stand on
//If there's no gravity then there's no up or down so naturally you can't stand on anything.
//For the same reason lattices in space don't count - those are things you grip, presumably.
/mob/proc/check_solid_ground()
	if(istype(loc, /turf/space))
		return 0

	if(!lastarea)
		lastarea = get_area(loc)
	if(!lastarea.has_gravity)
		return 0

	return 1

/mob/proc/Check_Dense_Object() //checks for anything to push off or grip in the vicinity. also handles magboots on gravity-less floors tiles

	var/shoegrip = Check_Shoegrip()

	for(var/turf/simulated/T in trange(1,src)) //we only care for non-space turfs
		if(T.density)	//walls work
			return 1
		else
			var/area/A = T.loc
			if(A.has_gravity || shoegrip)
				return 1

	for(var/obj/O in orange(1, src))
		if(istype(O, /obj/structure/lattice))
			return 1
		if(O && O.density && O.anchored)
			return 1

	return 0

/mob/proc/Check_Shoegrip()
	return 0

//return 1 if slipped, 0 otherwise
/mob/proc/handle_spaceslipping()
	if(prob(slip_chance(5)) && !buckled)
		to_chat(src, "<span class='warning'>You slipped!</span>")
		src.inertia_dir = src.last_move
		step(src, src.inertia_dir)
		return 1
	return 0

/mob/proc/slip_chance(var/prob_slip = 5)
	if(stat)
		return 0
	if(Check_Shoegrip())
		return 0
	return prob_slip

#define DO_MOVE(this_dir) var/final_dir = turn(this_dir, -dir2angle(dir)); Move(get_step(mob, final_dir), final_dir);

#define fastMove(dir) set instant = TRUE; \
	set hidden = TRUE; \
	moving_in_dir |= dir

#define fastStopMoving(dir) set instant = TRUE; \
	set hidden = TRUE; \
	moving_in_dir &= ~dir

/mob/proc/check_slipmove()
	return


/client/verb/moveup()
	set name = ".moveup"
	set instant = 1
	DO_MOVE(NORTH)

/client/verb/movedown()
	set name = ".movedown"
	set instant = 1
	DO_MOVE(SOUTH)

/client/verb/moveright()
	set name = ".moveright"
	set instant = 1
	DO_MOVE(EAST)

/client/verb/moveleft()
	set name = ".moveleft"
	set instant = 1
	DO_MOVE(WEST)
/*
/client/verb/fastNorth()
	fastMove(NORTH)
/client/verb/fastEast()
	fastMove(EAST)
/client/verb/fastSouth()
	fastMove(SOUTH)
/client/verb/fastWest()
	fastMove(WEST)

/client/verb/northUp()
	fastStopMoving(NORTH)
/client/verb/southUp()
	fastStopMoving(SOUTH)
/client/verb/eastUp()
	fastStopMoving(EAST)
/client/verb/westUp()
	fastStopMoving(WEST)
*/

#undef DO_MOVE



#undef fastMove
#undef fastStopMoving